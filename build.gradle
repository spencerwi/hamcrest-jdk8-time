apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
version = '0.5'
group = 'com.spencerwi'
archivesBaseName = 'hamcrest-jdk8-time'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

idea {
    module {
        downloadSources = true;
    }
}

task javadocJar(type: Jar, dependsOn: javadoc){
    from 'build/docs/javadoc'
    classifier = 'javadoc'
}

task sourcesJar(type: Jar){
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

if (project.hasProperty('isTravis') == false){
    signing {
        sign configurations.archives
    }
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/"){
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/"){
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'Hamcrest-JDK8-Time'
                    packaging 'jar'
                    description "Hamcrest matchers for Java 8's new java.time classes."
                    url "http://github.com/spencerwi/hamcrest-jdk8-time"

                    scm {
                        url 'scm:git@github.com:spencerwi/hamcrest-jdk8-time.git'
                        connection 'scm:git@github.com:spencerwi/hamcrest-jdk8-time.git'
                        developerConnection 'scm:git@github.com:spencerwi/hamcrest-jdk8-time.git'
                    }

                    licenses {
                        license {
                            name 'The MIT License (MIT)'
                            url 'https://raw.githubusercontent.com/spencerwi/hamcrest-jdk8-time/master/LICENSE'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'spencerwi'
                            name 'Spencer Williams'
                        }
                    }

                }
            }
        }
    }
}

